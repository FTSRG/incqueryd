grammar org.eclipse.incquery.patternlanguage.rdf.RdfPatternLanguage with org.eclipse.incquery.patternlanguage.PatternLanguage

import "http://www.eclipse.org/incquery/patternlanguage/PatternLanguage" as core
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate rdfPatternLanguage "http://www.eclipse.org/incquery/patternlanguage/rdf/RdfPatternLanguage"

RdfPatternModel returns core::PatternModel:
	{RdfPatternModel}
	base=Base?
	prefixes+=Prefix*
	patterns+=Pattern*
;

Base:
	'base' iri=RawIri
;

Prefix:
	'prefix' name=ID ':' iri=RawIri
;

Iri:
	(prefix=[Prefix] ':')? iri=RawIri
;

Constraint returns core::Constraint:
	PatternCompositionConstraint |
	CompareConstraint |
	RdfClassConstraint |
	RdfPropertyConstraint |
	RdfCheckConstraint
;

RdfClassConstraint:
	type=Type '(' variable=VariableReference ')'
;

Type returns core::EntityType:
	{RdfClass}
	class=Iri
;

RdfPropertyConstraint:
	refType=RefType '(' source=VariableReference ',' target=ValueReference ')'
;

RefType returns core::RelationType:
	{RdfProperty}
	property=Iri
;

RdfCheckConstraint:
	{RdfCheckConstraint}
	'check' '(' (variables+=VariableReference ',')* expression=STRING ')' // TODO find a better solution
;

ValueReference returns core::ValueReference:
	LiteralValueReference |
	VariableValue |
	AggregatedValue
;

LiteralValueReference returns core::LiteralValueReference:
	IntValue |
	DoubleValue |
	StringValue |
	BoolValue |
	RdfLiteral
;

RdfLiteral:
	lexicalForm=STRING (('^^' datatype=Iri) | ('@' language=ID))
;

RawIri returns ecore::EString:
	ID | ESCAPED_IRI
;

terminal ESCAPED_IRI:
	'<' ('\\>' | !'>')* '>'
;